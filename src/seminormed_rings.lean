import analysis.normed.normed_field
import analysis.special_functions.pow

noncomputable theory

open_locale topological_space

def is_pow_mult {Î± : Type*} [ring Î±] (f : Î± â†’ nnreal) :=
âˆ€ (a : Î±) {n : â„•} (hn : 1 â‰¤ n), f (a^n) = (f a) ^ n

structure is_seminorm {Î± : Type*} [ring Î±] (f : Î± â†’ nnreal) : Prop :=
(zero : f 0 = 0)
(add : âˆ€ a b, f (a + b) â‰¤ f a + f b)
(mul : âˆ€ a b, f (a * b) â‰¤ f a * f b)

def is_norm_le_one_class {Î± : Type*} [ring Î±] (f : Î± â†’ nnreal) : Prop := f 1 â‰¤  1

lemma is_seminorm.pow_le {Î± : Type*} [ring Î±] {f : Î± â†’ nnreal} (hf : is_seminorm f) (a : Î±) :
  âˆ€ {n : â„•}, 0 < n â†’ f (a ^ n) â‰¤ (f a) ^ n
| 1 h := by simp only [pow_one]
| (n + 2) h := by simpa [pow_succ _ (n + 1)] using le_trans (hf.mul a _)
    (mul_le_mul_left' (is_seminorm.pow_le n.succ_pos) _)

def is_norm_one_class {Î± : Type*} [ring Î±] (f : Î± â†’ nnreal) : Prop := f 1 = 1

lemma is_norm_one_class_iff_nontrivial {Î± : Type*} [ring Î±] {f : Î± â†’ nnreal} (hsn : is_seminorm f)
  (hf1 : f 1 â‰¤ 1) :
  is_norm_one_class f â†” âˆƒ x : Î±, f x â‰  0 :=
begin
  rw is_norm_one_class,
  refine âŸ¨Î» h, _, Î» h, _âŸ©,
  { use 1,
    rw h, exact one_ne_zero, },
  { obtain âŸ¨x, hxâŸ© := h,
    by_cases hf0 : f 1 = 0,
    { have hx' : f x â‰¤ 0,
      { rw â† mul_one x,
        apply le_trans (hsn.mul x 1) _,
        rw [hf0, mul_zero], },
      exact absurd (le_antisymm hx' (f x).2 ) hx, },
    { have h1 : f 1 * 1 â‰¤ f 1 * f 1,
      { conv_lhs{ rw â† one_mul (1 : Î±)},
        convert hsn.mul 1 1,
        rw mul_one, },
      rw mul_le_mul_left (lt_of_le_of_ne (zero_le (f 1)) (ne.symm hf0)) at h1,
      exact le_antisymm hf1 h1, }}
end

structure is_norm {Î± : Type*} [ring Î±] (f : Î± â†’ nnreal) extends (is_seminorm f) : Prop :=
(ne_zero : âˆ€ a, a â‰  0 â†’ 0 < f a)

structure is_algebra_norm (Î± : Type*) [comm_ring Î±] {g : Î± â†’ nnreal} (hg : is_norm g) 
  {Î² : Type*} [ring Î²] [algebra Î± Î²] (f : Î² â†’ nnreal) extends (is_norm f) : Prop :=
(smul : âˆ€ (a : Î±) (x : Î²) , f ((algebra_map Î± Î² a) * x) = g a * f x)

def function_extends {Î± : Type*} [comm_ring Î±] (g : Î± â†’ nnreal) {Î² : Type*} [ring Î²] [algebra Î± Î²]
  (f : Î² â†’ nnreal) : Prop :=
âˆ€ x : Î±, f (algebra_map Î± Î² x) = g x 

def is_nonarchimedean {Î± : Type*} [ring Î±] (f : Î± â†’ nnreal) : Prop := 
âˆ€ a b, f (a + b) â‰¤ max (f a) (f b)

lemma field.is_norm_of_is_seminorm {Î± : Type*} [field Î±] {f : Î± â†’ nnreal} (hf : is_seminorm f)
  (hnt : âˆƒ x : Î±, 0 â‰  f x) : is_norm f := 
{ ne_zero := Î» x hx, begin
    obtain âŸ¨c, hcâŸ© := hnt,
    have hfx : 0 â‰  f x,
    { intro h0,
      have hc' : f c â‰¤ 0,
      { rw [â† mul_one c, â† mul_inv_cancel hx, â† mul_assoc, mul_comm c, mul_assoc],
        refine le_trans (hf.mul x _) _,
        rw [â† h0, zero_mul] },
      exact hc (ge_antisymm hc' (zero_le (f c))), },
    exact lt_of_le_of_ne (zero_le (f _)) hfx,
  end,
  ..hf }
lemma seminormed_ring.to_is_seminorm (R : Type*) [semi_normed_ring R] :
  is_seminorm (Î» r : R, âˆ¥râˆ¥â‚Š) :=
{ zero := nnnorm_zero,
  add  := nnnorm_add_le,
  mul  := nnnorm_mul_le }

lemma normed_ring.to_is_norm (R : Type*) [normed_ring R] :
  is_norm (Î» r : R, âˆ¥râˆ¥â‚Š) :=
{ zero    := nnnorm_zero,
  add     := nnnorm_add_le,
  mul     := nnnorm_mul_le,
  ne_zero := Î» x hx, by { rw [pos_iff_ne_zero, ne.def, nnnorm_eq_zero], exact hx }}

def ring_hom.is_bounded {Î± : Type*} [semi_normed_ring Î±] {Î² : Type*} [semi_normed_ring Î²] 
  (f : Î± â†’+* Î²) : Prop := âˆƒ C : nnreal, 0 < C âˆ§ âˆ€ x : Î±, norm (f x) â‰¤ C * norm x

def ring_hom.is_bounded_wrt {Î± : Type*} [ring Î±] {Î² : Type*} [ring Î²] (nÎ± : Î± â†’ nnreal)
  (nÎ² : Î² â†’ nnreal) (f : Î± â†’+* Î²) : Prop :=
âˆƒ C : nnreal, 0 < C âˆ§ âˆ€ x : Î±, nÎ² (f x) â‰¤ C * nÎ± x

example {C : â„} (hC : 0 < C) : filter.tendsto (Î» n : â„•, C ^ (1 / (n : â„))) filter.at_top (ð“ 1) :=
begin
  apply filter.tendsto.comp _ (tendsto_const_div_at_top_nhds_0_nat 1),
  rw â† real.rpow_zero C,
  apply continuous_at.tendsto (real.continuous_at_const_rpow (ne_of_gt hC)),
end 

lemma contraction_of_is_pm_wrt {Î± : Type*} [ring Î±] {Î² : Type*} [ring Î²] {nÎ± : Î± â†’ nnreal}
  (hnÎ± : is_seminorm nÎ±) (nÎ² : Î² â†’ nnreal) (hÎ² : is_pow_mult nÎ²)
  {f : Î± â†’+* Î²} (hf : f.is_bounded_wrt nÎ± nÎ²) (x : Î±) : nÎ² (f x) â‰¤ nÎ± x :=
begin
  obtain âŸ¨C, hC0, hCâŸ© := hf,
  have hlim : filter.tendsto (Î» n : â„•, C ^ (1 / (n : â„)) * nÎ± x) filter.at_top (ð“ (nÎ± x)),
  { have : (ð“ (nÎ± x)) = (ð“ (1 * (nÎ± x))) := by rw one_mul,
    rw this,
    apply filter.tendsto.mul,
    { apply filter.tendsto.comp _ (tendsto_const_div_at_top_nhds_0_nat 1),
      rw â† nnreal.rpow_zero C,
      rw â† nnreal.tendsto_coe,
      apply continuous_at.tendsto (real.continuous_at_const_rpow (ne_of_gt hC0)), },
    exact tendsto_const_nhds, },
  apply ge_of_tendsto hlim,
  simp only [filter.eventually_at_top, ge_iff_le],
  use 1,
  intros n hn,
  have h : (C^(1/n : â„))^n  = C,
  { have hn0 : (n : â„) â‰  0 := nat.cast_ne_zero.mpr (ne_of_gt hn),
      rw [â† nnreal.rpow_nat_cast, â† nnreal.rpow_mul, one_div, inv_mul_cancel hn0,
        nnreal.rpow_one] },
  apply le_of_pow_le_pow n _ hn,
  { rw [mul_pow, h, â† hÎ² _ hn, â† ring_hom.map_pow],
    refine le_trans (hC (x^n)) (mul_le_mul (le_refl C)
      (hnÎ±.pow_le  _ (lt_of_lt_of_le zero_lt_one hn)) (zero_le _) (le_of_lt hC0)) },
    { exact zero_le _ },
end

lemma contraction_of_is_pm {Î± : Type*} [semi_normed_ring Î±] {Î² : Type*} [semi_normed_ring Î²] 
  (hÎ² : is_pow_mult (Î» x : Î², (âŸ¨âˆ¥xâˆ¥, norm_nonneg _âŸ© : nnreal))) {f : Î± â†’+* Î²} (hf : f.is_bounded)
  (x : Î±) : norm (f x) â‰¤ norm x :=
contraction_of_is_pm_wrt (seminormed_ring.to_is_seminorm Î±) (Î» x : Î², (âˆ¥xâˆ¥â‚Š))
  hÎ² hf x

lemma eq_seminorms  {Î± : Type*} [ring Î±] {f : Î± â†’ nnreal} (hf : is_seminorm f) (hfpm : is_pow_mult f)
  {g : Î± â†’ nnreal} (hg : is_seminorm g) (hgpm : is_pow_mult g)
  (hfg : âˆƒ (r : nnreal) (hr : 0 < r), âˆ€ (a : Î±), f a â‰¤ r * g a)
  (hgf : âˆƒ (r : nnreal) (hr : 0 < r), âˆ€ (a : Î±), g a â‰¤ r * f a) : f = g :=
begin
  obtain âŸ¨r, hr0, hrâŸ© := hfg,
  obtain âŸ¨s, hs0, hsâŸ© := hgf,
  have hle : ring_hom.is_bounded_wrt f g (ring_hom.id _) := âŸ¨s, hs0, hsâŸ©,
  have hge : ring_hom.is_bounded_wrt g f (ring_hom.id _) := âŸ¨r, hr0, hrâŸ©,
  ext x,
  exact le_antisymm (contraction_of_is_pm_wrt hg f hfpm hge x)
    (contraction_of_is_pm_wrt hf g hgpm hle x),
end